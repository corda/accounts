import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'idea'


repositories {
    mavenLocal()
    maven { url "$publicArtifactURL/freighter-dev" }
}

sourceSets {
    freighterTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/freighterTest/kotlin')
        }
    }
}

task freighterTest(type: Test, dependsOn: []) {
    maxParallelForks 1
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "128"
    testClassesDirs = sourceSets.freighterTest.output.classesDirs
    classpath = sourceSets.freighterTest.runtimeClasspath
    useJUnitPlatform {
        includeTags "DOCKER"
        excludeTags "AZURE", "ORACLE", "FULL_LINUX_KERNEL"
    }
}

configurations {
    freighterTestImplementation.extendsFrom testImplementation
    freighterTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    freighterTestImplementation "freighter:freighter-testing-core-junit5:0.7.1-SNAPSHOT"

    freighterTestImplementation project(":contracts")
    freighterTestImplementation project(":workflows")
}
