import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'kotlin'
apply plugin: 'idea'


repositories {
    mavenLocal()
    maven { url "https://software.r3.com/artifactory/freighter-dev" }
}

sourceSets {
    freighterTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/freighterTest/kotlin')
        }
    }
}

task freighterTest(type: Test, dependsOn: []) {
    maxParallelForks 1
    systemProperty "java.util.concurrent.ForkJoinPool.common.parallelism", "128"
    testClassesDirs = sourceSets.freighterTest.output.classesDirs
    classpath = sourceSets.freighterTest.runtimeClasspath
    useJUnitPlatform {
        includeTags "DOCKER"
        excludeTags "AZURE", "ORACLE", "FULL_LINUX_KERNEL"
    }
}

configurations {
    freighterTestCompile.extendsFrom testCompile
    freighterTestRuntime.extendsFrom testRuntime
}

dependencies {
    freighterTestCompile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    freighterTestCompile "freighter:freighter-testing-core-junit5:0.7.1-SNAPSHOT"

    freighterTestCompile project(":contracts")
    freighterTestCompile project(":workflows")
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}


