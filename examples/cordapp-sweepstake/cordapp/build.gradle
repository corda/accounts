apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    targetPlatformVersion 14
    minimumPlatformVersion 14
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    // Accounts
    cordapp project(":workflows")
    cordapp project(":contracts")
    cordapp project(":examples:cordapp-sweepstake:states")
    cordapp project(":examples:cordapp-sweepstake:flows")

    // Corda
    corda "$corda_release_group:corda:$corda_release_version"
    cordaProvided "$corda_release_group:corda-core:$corda_release_version"
    cordaProvided "$corda_release_group:corda-rpc:$corda_release_version"
    cordaProvided "$corda_release_group:corda-jackson:$corda_release_version"

    // Tokens
    cordapp "$tokens_release_group:tokens-contracts:$tokens_release_version"
    cordapp "$tokens_release_group:tokens-workflows:$tokens_release_version"

    // Spring
    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }

    // JSON parser
    implementation "com.beust:klaxon:5.0.1"

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // Testing
    testImplementation project(":examples:cordapp-sweepstake:states")
    testImplementation project(":examples:cordapp-sweepstake:flows")
    testImplementation "$corda_release_group:corda-node:$corda_release_version"
    testImplementation "$corda_release_group:corda-node-driver:$corda_release_version"
    testImplementation "$corda_release_group:corda-test-common:$corda_release_version"
    testImplementation "$corda_release_group:corda-core-test-utils:$corda_release_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "junit:junit:$junit_version"
    testImplementation ("org.springframework.boot:spring-boot-starter-test:2.0.2.RELEASE") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }


}

task deployNodesX(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {

    nodeDefaults {
        projectCordapp {
            deploy = false
        }
        cordapp project(":workflows")
        cordapp project(":contracts")
        cordapp project(":examples:cordapp-sweepstake:states")
        cordapp project(":examples:cordapp-sweepstake:flows")
        cordapp "$tokens_release_group:tokens-contracts:$tokens_release_version"
        cordapp "$tokens_release_group:tokens-workflows:$tokens_release_version"
        cordapp "$tokens_release_group:tokens-money:$tokens_release_version"
    }

    directory "./build/nodes"
    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating: false]
        p2pPort 10000
        sshdPort 2222
        rpcSettings {
            address("localhost:10001")
            adminAddress("localhost:10002")
        }
    }
    node {
        name "O=TournamentA,L=London,C=GB"
        p2pPort 10004
        sshdPort 2223
        rpcSettings {
            address("localhost:10005")
            adminAddress("localhost:10006")
        }
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=TournamentB,L=London,C=GB"
        p2pPort 10008
        sshdPort 2224
        rpcSettings {
            address("localhost:10009")
            adminAddress("localhost:10010")
        }
        rpcUsers = [[user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    jvmArgs test_module_opens
    jvmArgs test_module_exports
}

task runTournamentServer(type: JavaExec, dependsOn: assemble) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.r3.corda.lib.accounts.examples.sweepstake.cordapp.clients.WorldCupApplication'
    args '--server.port=8080', '--config.rpc.host=localhost', '--config.rpc.port=10005', '--config.rpc.username=user1', '--config.rpc.password=test'
}
